## 指令跳转

CPU是通过CS:IP来获取下一条指令的值，那么通过指令修改CS、IP这两个寄存器的值，便可以控制CPU所执行的指令了。可由于控制CPU执行指令的CS、IP十分的关键，因此8086并不允许像其它普通的寄存器一般使用mov等指令对CS、IP修改(mov IP,1000H是非法的)，**而是提供了专门的指令来控制CS、IP的值，这一类指令被称为8086跳转指令**

### 无条件跳转指令（jmp）

jmp既可以只修改IP，也可以同时修改CS和IP，作为跳转指令，在编程时需要指定跳转的位置，进而修改CS/IP的值

**段内转移指令**

段内短转移(IP 变化-128~127)：段内短转移的格式为 jmp short [标号]

```assembly
assume cs:codesg

codesg segment
    start:mov ax,0
          jmp short s
          add ax,1
        s:inc ax
codesg ends
 
end start
```

**段内近转移**

段内近转移(IP 变化-32768~32767)：当所要跳转的间隔大于短转移的时候，就需要使用段内近转移。段内近转移和短转移类似，格式为 jmp near ptr [标号]，段内转移只修改IP，不修改CS的值

**段间转移**

当跳转的间隔超过了段内近转移的限制时，就需要使用段间转移了。段间转移的格式为 jmp far ptr [标号]，和内存寻址一样，jmp指令所要跳转的位置也可以通过寄存器或是指令中的立即数指定

**jmp 寄存器跳转**

jmp [16位寄存器]，例如 jmp ax，寄存器跳转属于段内跳转

**jmp 内存跳转**

jmp word ptr [内存单元地址] 例如： jmp word ptr 2345H，jmp word ptr [bx] ，[ ]内只要是符合内存寻址方式的语法皆可。jmp word ptr处理的是16位数，属于段内转移

jmp dword ptr [内存单元地址] jmp dword ptr 和 jmp word ptr类似，只不过会将对应地址的处的两个字/四字节的数据作为偏移地址，其中 IP 等于指定的内存地址，C S等于指定的内存地址+2，jmp dword ptr处理的是32位数，属于段间转移

**总结**

- 段内转移是通过相对地址偏移量来控制的。段内短转移可以使得IP偏移2^8的范围，即(-128~127)，而段内近转移可以使得IP偏移2^16的范围，即(-32768~32767)

- 8086的CPU是16位的，在20位的寻址范围内进行更大幅度的跳转，16位的偏移地址是不够的，因此段间转移的指令是通过绝对地址来实现的
- 理论上段内转移都可以使用段间转移来实现，但是由于不同的跳转指令所占用的内存空间是不一样的（段内短转移=8位指令+8位偏移地址=16bit，段内近转移=8位指令+16位偏移地址=24bit，段间转移=8位指令+16位段地址+16位偏移地址=40bit）。所以编程时，在满足需求的前提下还是尽可能的使用更简单，更节约内存的无条件跳转指令，提高效率

### 有条件跳转指令

jcxz（jmp if cx is zero），类似于段内短跳转 jmp short，格式为 jcxz [标号]，唯一的不同在于，只有当满足寄存区 cx = 0时，才会进行跳转，否则 ip 自增

### 循环指令

循环指令同样依赖寄存器cx，格式为loop [标号]

loop指令的语义是，首先将cx自减1，如果cx不为0，则跳转至标号处，否则什么也不做，离开循环，顺序执行下移，循环指令的跳转范围和有条件跳转指令一样，ip的变化范围为(-128~127)

## 子过程

**定义**

在有的语言中被称为函数、方法或者过程，而在8086汇编中被称为子程序

**区别**

过程跳转和无条件跳转的不同之处在于，跳转的子程序执行完毕后，还需要能够正确的返回子程序执行完成后的第一条指令上，执行之后的程序

**过程**

程序跳转时，可以将当前的CS:IP值压入栈中，当子程序执行完毕后再将栈中的CS:IP弹出。栈的先进后出的特性使得栈这一结构可以很好的完成任务

### **过程跳转指令**

过程跳转指令通过将CS:IP的压栈/出栈和之后的跳转合而为一，降低了使用子程序时的复杂度

8086 汇编的子程序跳转指令可以分为两类，一是**子程序调用指令**，二是**子程序返回指令**

#### 子程序调用指令

call 指令，调用格式

```assembly
call [标号]：其相当于push IP；jmp near ptr [标号]。是段内转移，位移的值由编译器在编译时根据标号位置动态指定，偏移的IP范围也如jmp near一致(-32678~32767)

call far ptr [标号]：其相当于 push CS；push IP；jmp far ptr [标号]

call [16位寄存器]：相当于push IP；jmp near [16位寄存器]

call word ptr [内存单元地址]： 相当于 push IP; jmp word ptr [内存单元地址]

call dword ptr [内存单元地址]： 相当于push IP; jmp dword ptr [内存单元地址]
```

#### 子程序返回指令

ret 指令，先将栈中的CS/IP弹出，覆盖还原调用者在调用子程序跳转前的CS/IP值，再进行跳转，这样便能够正确的返回子程序执行完毕后调用者对应的指令处。

```assembly
ret 指令： 其相当于pop IP;弹出栈中的一个数据，用于复原IP的值，从而实现近转移

ret n 指令：类似ret，在ret的基础上进行了栈顶指针sp的偏移(例如 ret 4)，相当于pop IP;add sp,n 

retf 指令： 其相当于pop IP; pop CS;(和call far ptr的入栈顺序正好相反)弹出栈中的两个数据，分别用于复原CS、IP的值，从而实现远转移。

retf n 指令：类似retf，在retf的基础上进行了栈顶指针sp的偏移(例如 retf 4)，相当于pop IP;pop CS;add sp,n 
```

#### call 和 ret 组合使用

```assembly
assume cs:code
code segment
main: ..
      ..
      call sub1; 调用sub1子程序
      ..
      ..
      mov ax,4c00h
      int 21h
sub1: ..
      ..
      call sub2; 调用sub2子程序
      ..
      ..
      ret; sub1子程序返回
sub2: ..
      ..
      ..
      ret; sub2子程序返回
code ends
end main
```

### 子程序与调用者之间参数/返回值传递的问题

参数返回值传递的问题解决方法其实质是如何通过某一媒介，使得调用者和子程序都能访问到其中的数据。这一媒介主要有三种：**寄存器、通用内存**以及**栈**

**寄存器**

```assembly
;说明:计算N的三次方
;参数:（bx)=N
;返回值: (dx:ax)=N^3
cube:mov ax,bx
     mul bx; mul bx可以简单理解为ax = ax * bx
     mul bx
     ret
```

**通用内存**

```assembly
;说明:将ascll字母转为大写
;参数: 将（ds:si）指向的内存单元中的字母转为大写
data segment
    db 'helloworld'
data ends

code segment
start:
    mov ax,data
    mov ds,ax
    mov si,0
    mov cx,10; 'helloworld'的长度
    call capital
    mov ax,4c00h
    int 21h
capital:
    and byte ptr [si],11011111b; 利用字母大小写ascll码的规律进行大小写转换
    inc si; si指向下一个内存单元
    loop capital
    ret 
code ends
end start
```

**栈**

使用栈来传递参数比起使用通用内存来说具有几个优点：

- 易于理解
- 子程序与调用者之间存在着共享寄存器冲突的问题，通常使用栈来缓存子程序与调用者冲突的寄存器内容
- 适合临时局部变量的生命周期，作用域的场景

```assembly
assume cs:code

code segment
start:
; 参数b先压入栈中，参数a后压入栈中
    mov ax,1
    push ax
    mov ax,3
    push ax
    call difcube
    mov ax,4c00h
    int 21h
; difcube 计算两数之差的立方 依赖子程序cube
; 参数a=[sp+4];b=[sp+6] （call指令会将当前IP压入栈中，因此IP=[sp+2],栈中元素占用两个内存单元）
; 返回值 ax = (a-b)^3
difcube:
    push bp
    mov bp,sp
    mov ax,[bp+4]
    sub ax,[bp+6]
    push ax
    call cube
    pop bp
    ret 4; ret时需要将进行sp的偏移(参数个数为2，偏移量为4)，将参数弹出栈中，使得程序得以正确的返回
; cube 计算N的立方
; 参数n=[sp+4]
; 返回值 ax = n^3
cube:
    push bp
    mov bp,sp
    mov bx,[bp+4]
    mov ax,bx
    mul bx
    mul bx
    pop bp
    ret 2; ret时需要将进行sp的偏移(参数个数为1，偏移量为2)，将参数弹出栈中，使得程序得以正确的返回
code ends
end start
```
